buildscript {
  repositories {
    mavenCentral()
    maven { url 'https://s01.oss.sonatype.org' }
    gradlePluginPortal()
    mavenLocal()
    google()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
  }
  dependencies {
    classpath "com.android.tools.build:gradle:8.7.3"
  }
}

plugins {
  id 'java-library'
}

repositories {
  mavenCentral()
}

dependencies {
  testImplementation platform('org.junit:junit-bom:5.10.0')
  testImplementation 'org.junit.jupiter:junit-jupiter'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
  useJUnitPlatform()
}

allprojects {
  apply plugin: 'eclipse'
  apply plugin: 'idea'

  // This allows you to "Build and run using IntelliJ IDEA", an option in IDEA's Settings.
  idea {
    module {
      outputDir file('build/classes/java/main')
      testOutputDir file('build/classes/java/test')
    }
  }
}

configure(subprojects.findAll { p -> p.name == 'core' || p.name == 'jvm' }) {
  apply plugin: 'java-library'
  java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
  }
}

// These modules are currently NOT configured in the root build.gradle for java-library
// but they apply it themselves.

configure(subprojects.findAll { p -> p.name != 'core' }) {
  afterEvaluate {
    if (it.plugins.hasPlugin('java-library') || it.plugins.hasPlugin('com.android.library')) {
      dependencies {
        api rootProject.project(':core')
      }
    } else if (it.plugins.hasPlugin('java')) {
      dependencies {
        implementation rootProject.project(':core')
      }
    }
  }
}

configure(subprojects.findAll { !it.path.contains('android') }) {
  // From https://lyze.dev/2021/04/29/libGDX-Internal-Assets-List/
  // The article can be helpful when using assets.txt in your project.
  tasks.register('generateAssetList') {
    inputs.dir("${project.rootDir}/assets/")
    // projectFolder/assets
    File assetsFolder = new File("${project.rootDir}/assets/")
    // projectFolder/assets/assets.txt
    File assetsFile = new File(assetsFolder, "assets.txt")
    // delete that file in case we've already created it
    assetsFile.delete()

    // iterate through all files inside that folder
    // convert it to a relative path
    // and append it to the file assets.txt
    fileTree(assetsFolder).collect { assetsFolder.relativePath(it) }.sort().each {
      assetsFile.append(it + "\n")
    }
  }
  processResources.dependsOn 'generateAssetList'

  compileJava {
    options.incremental = true
  }
}

configure(subprojects.findAll { p -> p.name != 'core' && p.name != 'jvm' && p.name != 'teavm' }) {
  afterEvaluate {
    if (it.plugins.hasPlugin('java-library') || it.plugins.hasPlugin('com.android.library')) {
      dependencies {
        api rootProject.project(':jvm')
      }
    } else if (it.plugins.hasPlugin('java')) {
      dependencies {
        implementation rootProject.project(':jvm')
      }
    }
  }
}

subprojects {
  group = 'me.stringdotjar.flixelgdx'
  version = "$projectVersion"
  ext.appName = 'flixelgdx'
  repositories {
    mavenCentral()
    gradlePluginPortal()
    maven { url 'https://s01.oss.sonatype.org' }
    // You may want to remove the following line if you have errors downloading dependencies.
    mavenLocal()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://jitpack.io' }
  }

  // Force Java 17 for the entire framework.
  if (!project.path.contains('android')) {
    plugins.withType(JavaPlugin).tap {
      configureEach {
        java {
          toolchain {
            languageVersion = JavaLanguageVersion.of(17)
          }
        }
      }
    }
  }
}

eclipse.project.name = 'flixelgdx' + '-parent'

subprojects {
  if (project.plugins.hasPlugin('java-library') || project.plugins.hasPlugin('java')) {
    processResources.dependsOn 'generateAssetList'
    compileJava {
      options.incremental = true
    }
  }
}
